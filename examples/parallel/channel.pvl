class Channel {

	boolean transfering;
    
    int exchangeValue;
    
    seq<int> sent, recvd;
    
    resource lock_invariant() = 
		Perm(transfering, 1) 
		** Perm(exchangeValue,1)
		** Perm(sent, 1\2)
		** Perm(recvd, 1\2)
		** (transfering	==> sent == recvd)
		** (!transfering ==> sent == recvd ++ exchangeValue)
		;
    
    ensures Perm(sent, 1\2) ** Perm(recvd, 1\2);
	ensures |sent| == 0;
	ensures |recvd| == 0;
    Channel() {
		transfering = true;
		sent = seq<int> {};
		recvd = seq<int> {};
    }
 
	context Perm(sent, 1\2);
	ensures sent == \old(sent) ++ v;
    void writeValue(int v) {
		lock this;
		loop_invariant Perm(transfering, 1) ** Perm(exchangeValue,1);
		loop_invariant held(this);
		loop_invariant Perm(sent, 1) ** Perm(recvd, 1\2);
		loop_invariant (transfering	==> sent == recvd);
		loop_invariant (!transfering ==> sent == recvd ++ exchangeValue);
		loop_invariant sent == \old(sent);
		while (!transfering) {
			wait this;
        }
        transfering = false;
        exchangeValue = v;
        sent = sent ++ v;
        notify this;
        unlock this;
    }
 
	context Perm(recvd, 1\2);
	ensures recvd == \old(recvd) ++ \result;
	ensures |recvd| == \old(|recvd|) + 1;
    int readValue() {
		lock this;
		loop_invariant Perm(transfering, 1) ** Perm(exchangeValue,1);
		loop_invariant held(this);
		loop_invariant Perm(sent, 1\2) ** Perm(recvd, 1);
		loop_invariant (transfering	==> sent == recvd);
		loop_invariant (!transfering ==> sent == recvd ++ exchangeValue);
		loop_invariant recvd == \old(recvd);
        while (transfering) {
			wait this;
        }
        int v = exchangeValue;
        recvd = recvd ++ v;
        transfering = true;
        notify this;
        unlock this;
        return v;
    } 
}

